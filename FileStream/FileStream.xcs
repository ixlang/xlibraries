package FileStream {
//xlang 

	static class FileInputStream : Stream{
		long filehandler;

		FileInputStream(String path){
			filehandler = XPlatform.openSystemFile(path, "r");
			if (filehandler == 0){
				throw new IllegalArgumentException("file " + path + " can not open");
			}
		} 
		FileInputStream(FileSystem.File file){
			filehandler = XPlatform.openSystemFile(file.getPath(), "r");
			if (filehandler == 0){
				throw new IllegalArgumentException("file " + file.getPath() + " can not open");
			}
		}
		bool attach(String path){
			if (filehandler != 0){
				_system_.closeFile(filehandler);
				filehandler = 0;
			}
			filehandler = XPlatform.openSystemFile(path, "r");
			return filehandler != 0;
		}
        
		void checkValid(){
			if (filehandler == 0){
				throw new IllegalArgumentException("The Stream is not ready");
			}
		}
		long available(bool bwait){
			return length() - getPosition();
		}
		long seek(int type, long pos){
			checkValid();
			return _system_.seekFile(filehandler, type, pos);
		}
		
		long getPosition(){
			checkValid();
			return _system_.getFileSeek(filehandler);
		}
		
		long length(){
			checkValid();
			return _system_.fileLength(filehandler);
		}

		byte[] read(){
			checkValid();
			byte[] data = new byte[length()];
			_system_.readFile(filehandler, data, 0, data.length);
			return data;
		}

		int read(byte [] data , int position, int length)override{
			checkValid();
			return _system_.readFile(filehandler, data, position, length);
		}

		int write(byte [] data , int position, int length)override{
			return 0;
		}
		
		void close(){
			if (filehandler != 0){
				_system_.closeFile(filehandler);
				filehandler = 0;
			}
		}
		
		void finalize(){
			close();
		}
	};
		
	static class FileOutputStream : Stream{
		long filehandler;

		FileOutputStream(String path){
			filehandler = XPlatform.openSystemFile(path, "w");
			if (filehandler == 0){
				throw new IllegalArgumentException("file " + path + " can not open");
			}
		}
		FileOutputStream(String path, bool append){
			filehandler = XPlatform.openSystemFile(path, append ? "a" : "w");
			if (filehandler == 0){
				throw new IllegalArgumentException("file " + path + " can not open");
			}
		}
        
		FileOutputStream(FileSystem.File file){
			filehandler = XPlatform.openSystemFile(file.getPath(), "w");
			if (filehandler == 0){
				throw new IllegalArgumentException("file " + file.getPath() + " can not open");
			}
		}
		FileOutputStream(FileSystem.File file, bool append){
			filehandler = XPlatform.openSystemFile(file.getPath(), append ? "a" : "w");
			if (filehandler == 0){
				throw new IllegalArgumentException("file " + file.getPath() + " can not open");
			}
		}
		bool attach(String path){
			if (filehandler != 0){
				_system_.closeFile(filehandler);
				filehandler = 0;
			}
			filehandler = XPlatform.openSystemFile(path, "w");
			return filehandler != 0;
		}
		void checkValid(){
			if (filehandler == 0){
				throw new IllegalArgumentException("The Stream is not ready");
			}
		}
		long available(bool bwait){
			return 1;
		}
		long seek(int type, long pos){
			return 0;
		}
		
		long getPosition(){
			return 0;
		}
		
		long length(){
			checkValid();
			return 0;
		}

		byte[] read(){
			return nilptr;
		}

		int read(byte [] data , int position, int length)override{
			return 0;
		}
		
		long write(byte [] data){
			checkValid();
			return _system_.writeFile(filehandler, data, 0, data.length);
		}
		
		int write(byte [] data , int position, int length)override{
			checkValid();
			return _system_.writeFile(filehandler, data, position, length);
		}
		
		void close(){
			if (filehandler != 0){
				_system_.closeFile(filehandler);
				filehandler = 0;
			}
		}
        
		void finalize(){
			close();
		}
	};
};